import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

buildscript {
    repositories {
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.github.jk1:gradle-license-report:3.0.1'
    }
}

plugins {
    id("com.vanniktech.maven.publish") version "0.36.0"
}

def appAndSourceUrl = 'https://github.com/TNG/value-provider'
def javaVersionString = project.hasProperty('javaVersion')
        ? project.property('javaVersion').toString()
        : '21'
def javaLanguageVersion = JavaLanguageVersion.of(javaVersionString)

ext {
    app = [
            description: 'A library that facilitates writing realistic test data and in turn better tests for your Java application.',
            name       : 'value-provider',
            urls       : [
                    entry : appAndSourceUrl,
                    doc   : appAndSourceUrl,
                    issues: "${appAndSourceUrl}/issues",
                    source: appAndSourceUrl
            ],
            gitRepo    : 'git@github.com:TNG/value-provider.git',
            license    : [
                    name: 'The Apache Software License, Version 2.0',
                    url : 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            ]
    ]
    company = [
            name: 'TNG Technology Consulting GmbH',
            url : 'https://www.tngtech.com/'
    ]

    productionProjects = [
            project(':core'),
            project(':example'),
            project(':junit4'),
            project(':junit5')]
    releaseProjects = [
            project(':core'),
            project(':junit4'),
            project(':junit5')]
    createModuleDescription = { description, proj -> "${description} - Module '${proj.name}'" }

    currentScriptRootOf = { it.buildscript.sourceFile.parentFile }
}

allprojects {
    group = 'com.tngtech.valueprovider'
    version = '1.4.4-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    java {
        toolchain {
            languageVersion = javaLanguageVersion
        }

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        if (releaseProjects.contains(project)) {
            withJavadocJar()
            withSourcesJar()
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    dependencies {
        implementation libs.guava
        implementation libs.slf4j.api

        testImplementation libs.apache.commons
        testImplementation libs.assertj.core
        testImplementation libs.junit4
        testImplementation libs.junit.jupiter.api

        // hint: activate (debug) logging via system property -Dorg.slf4j.simpleLogger.defaultLogLevel=debug
        testRuntimeOnly libs.slf4j.simple
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}

ext.isReleaseVersion = !project.version.endsWith("-SNAPSHOT")

// duplication seems inevitable, as buildscript {...} does not support variables
def isReleaseBuild = project.hasProperty('release')

releaseProjects*.with {
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false // the meta-data does not match the way the Maven artifacts are composed and thus is broken
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xdoclint:none', '-quiet')
        // html tags are only supported in Java 9+ Javadoc
        if (javaLanguageVersion.canCompileOrRun(9)) {
            options.addBooleanOption('html5', true)
        }
    }

    project(":core") {
        description = app.description
    }

    project(":junit4") {
        description = 'JUnit 4 test infrastructure to reproduce random test data in case of test failures.'
    }

    project(":junit5") {
        description = 'JUnit 5 test infrastructure to reproduce random test data in case of test failures.'
    }

    project(":example") {
        description = 'Examples for test data factories using the value-provider library.'
    }

    tasks.withType(Jar).configureEach {
        from(rootProject.rootDir) {
            include("LICENSE", "NOTICE")
            into("META-INF")
        }

        archiveBaseName = 'value-provider-' + project.name

        manifest {
            def title = archiveBaseName.get()
            def now = LocalDate.now()
            //noinspection UnnecessaryQualifiedReference -> false positive
            def today = now.format(java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd"))
            def companyName = company.name
            def copyright = "${now.year} ${companyName}"

            attributes(
                    'Built-By': "Gradle ${gradle.gradleVersion}",
                    'Built-Date': today,
                    'Specification-Title': title,
                    'Specification-Version': archiveVersion,
                    'Specification-Vendor': companyName,
                    'Implementation-Title': title,
                    'Implementation-Version': archiveVersion,
                    'Implementation-Vendor': company.name,
                    'Issue-Tracker': "https://github.com/TNG/value-provider/issues",
                    'Documentation-URL': "https://github.com/TNG/value-provider",
                    'Copyright': copyright,
                    'License': app.license.name,
            )
        }
    }

    if (isReleaseBuild) {
        apply plugin: 'com.vanniktech.maven.publish'
        apply plugin: 'signing'

        tasks.withType(AbstractPublishToMaven).configureEach {
            it.dependsOn(build)
        }
        tasks.withType(PublishToMavenRepository).configureEach {
            it.doFirst {
                assert !gradle.startParameter.isParallelProjectExecutionEnabled():
                        'uploading archives with parallel execution seems to lead to broken uploads in Maven Central'
            }
        }

        mavenPublishing {
            // None -> do NOT create Javadoc jar, but publish the one created via standard withJavadocJar()' instead
            // true -> publish sources jar
            configure(new JavaLibrary(new JavadocJar.None(), true))
            // true -> automatically publish after uploading
            publishToMavenCentral(true)
            signAllPublications()

            coordinates(
                    project.group.toString(),
                    'value-provider-' + project.name,
                    project.version.toString()
            )

            pom {
                name = 'value-provider-' + project.name
                packaging = "jar"
                description = project.description
                url = app.urls.entry

                developers {
                    developer {
                        id = 'stefanhechtltng'
                        name = 'Stefan Hechtl'
                        email = 'stefan.hechtl@tngtech.com'
                    }
                    developer {
                        id = 'jonashoef'
                        name = 'Jonas HÃ¶f'
                        email = 'jonas.hoef@tngtech.com'
                    }
                }

                licenses {
                    license {
                        name = app.license.name
                        url = app.license.url
                        distribution = 'repo'
                    }
                }

                organization {
                    name = company.name
                    url = company.url
                }

                scm {
                    url = app.urls.source
                    connection = "scm:${app.gitRepo}"
                    developerConnection = "scm:${app.gitRepo}"
                }
            }
        }

        signing {
            // requires gradle.properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html
            required = isReleaseVersion

            def signingKey = findProperty("signingKey")
            def signingPassword = findProperty("signingPassword")
            useInMemoryPgpKeys(signingKey, signingPassword)
        }
    }
}
apply plugin: 'com.github.jk1.dependency-license-report'
