buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.github.jk1:gradle-license-report:1.16'
    }
}

plugins {
    id "de.marcphilipp.nexus-publish" version "0.4.0" apply false
}

def appAndSourceUrl = 'https://github.com/TNG/value-provider'

ext {
    year = "${Calendar.getInstance().get(Calendar.YEAR)}"
    app = [
            name   : 'value-provider',
            urls   : [
                    entry : appAndSourceUrl,
                    doc   : appAndSourceUrl,
                    issues: "${appAndSourceUrl}/issues",
                    source: appAndSourceUrl
            ],
            gitRepo: 'git@github.com:TNG/value-provider.git',
            license: [
                    name: 'The Apache Software License, Version 2.0',
                    url : 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            ]
    ]
    company = [
            name: 'TNG Technology Consulting GmbH',
            url : 'https://www.tngtech.com/'
    ]

    dependency = [
            apache_commons       : [group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'],
            guava                : [group: 'com.google.guava', name: 'guava', version: '30.1-jre'],
            slf4j                : [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'],
            lombok               : [group: 'org.projectlombok', name: 'lombok', version: '1.18.20'],

            junit4               : [group: 'junit', name: 'junit', version: '4.13.1'],
            junit4_dataprovider  : [group: 'com.tngtech.junit.dataprovider', name: 'junit4-dataprovider', version: '2.7'],
            junit5_dataprovider  : [group: 'com.tngtech.junit.dataprovider', name: 'junit-jupiter-dataprovider', version: '2.7'],
            assertj_core         : [group: 'org.assertj', name: 'assertj-core', version: '3.19.0'],
            mockito              : [group: 'org.mockito', name: 'mockito-core', version: '3.7.7'],
            log4j_api            : [group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'],
            log4j_core           : [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'],
            log4j_slf4j          : [group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.0'],

            junit4_engine        : [group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.7.0'],
            junit_platform_runner: [group: 'org.junit.platform', name: 'junit-platform-runner', version: '5.7.0'],
            junit_jupiter_api    : [group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'],
            junit_jupiter_engine : [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'],
            junit_jupiter_params : [group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.0'],
            junit_jupiter_testkit: [group: 'org.junit.platform', name: 'junit-platform-testkit', version: '1.7.0']
    ]

    postfixedJar = { File jarFile, String postfix ->
        new File(jarFile.parentFile, jarFile.name.replaceAll(/\.jar$/, "-${postfix}.jar"))
    }

    tempJar = { File jarFile -> postfixedJar(jarFile, 'tmp') }

    productionProjects = [
            project(':value-provider-core'),
            project(':value-provider-example'),
            project(':value-provider-junit4'),
            project(':value-provider-junit5')]
    releaseProjects = [
            project(':value-provider-core'),
            project(':value-provider-junit4'),
            project(':value-provider-junit5')]
    createModuleDescription = { description, proj -> "${description} - Module '${proj.name}'" }

    currentScriptRootOf = { it.buildscript.sourceFile.parentFile }
}

allprojects {
    group = 'com.tngtech.valueprovider'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

description = 'A library that facilitates writing realistic test data and in turn better tests for your Java application.'

task showJdkVersion {
    println "Configured JDK: ${JavaVersion.current()}"
}

task clean {
    doLast {
        project.buildDir.deleteDir()
    }
}

subprojects {
    apply plugin: 'java-library'
    compileJava { options.encoding = "UTF-8" }
    compileTestJava { options.encoding = "UTF-8" }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        implementation dependency.apache_commons
        implementation dependency.guava
        implementation dependency.slf4j

        testImplementation dependency.assertj_core
        testImplementation dependency.junit4
        testImplementation dependency.junit_jupiter_api

        testRuntimeOnly dependency.log4j_slf4j
        testRuntimeOnly dependency.log4j_api
        testRuntimeOnly dependency.log4j_core
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}

ext.isReleaseVersion = !project.version.endsWith("-SNAPSHOT")

if (!hasProperty("sonatypeUsername")) {
    ext.sonatypeUsername = ""
}
if (!hasProperty("sonatypePassword")) {
    ext.sonatypePassword = ""
}

releaseProjects*.with {
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "de.marcphilipp.nexus-publish"

    tasks.withType(GenerateModuleMetadata) {
        enabled = false // the meta-data does not match the way the Maven artifacts are composed and thus is broken
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    // fix for broken java doc generation when using html tags in javadoc
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
                options.addBooleanOption('html5', true)
            }
        }
    }

    tasks.withType(AbstractPublishToMaven) {
        it.dependsOn(build)
    }
    tasks.withType(PublishToMavenRepository) {
        it.doFirst {
            assert !gradle.startParameter.isParallelProjectExecutionEnabled():
                    'uploading archives with parallel execution seems to lead to broken uploads in Sonatype Nexus'
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.archivesBaseName
                from components.java
                pom {
                    name = app.name
                    packaging = "jar"
                    description = project.description
                    url = app.urls.entry

                    developers {
                        developer {
                            id = 'stefanhechtltng'
                            name = 'Stefan Hechtl'
                            email = 'stefan.hechtl@tngtech.com'
                        }
                        developer {
                            id = 'jonashoef'
                            name = 'Jonas HÃ¶f'
                            email = 'jonas.hoef@tngtech.com'
                        }
                    }

                    organization {
                        name = company.name
                        url = company.url
                    }

                    scm {
                        url = app.urls.source
                        connection = "scm:${app.gitRepo}"
                        developerConnection = "scm:${app.gitRepo}"
                    }
                }
            }
        }

        // respective username and password can be configured in ~/.gradle/gradle.properties
        if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            nexusPublishing {
                repositories {
                    sonatype()
                }
            }
        }
    }

    signing {
        // requires gradle.properties, see http://www.gradle.org/docs/current/userguide/signing_plugin.html
        required {
            isReleaseVersion && gradle.taskGraph.hasTask('publish') && project.hasProperty('sonatypeUsername')
        }
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}

apply plugin: 'com.github.jk1.dependency-license-report'